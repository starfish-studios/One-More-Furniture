plugins {
	id 'dev.architectury.loom' version '1.7-SNAPSHOT' apply false
	id 'architectury-plugin' version '3.4-SNAPSHOT'
	id 'com.gradleup.shadow' version '8.3.6' apply false
}

architectury {
	minecraft = rootProject.minecraft_version
}

subprojects {
	apply plugin: 'dev.architectury.loom'
	apply plugin: 'architectury-plugin'
	apply plugin: 'maven-publish'

	base {
		archivesName = "$rootProject.archives_base_name-$project.name"
	}

	loom {
		silentMojangMappingsLicense()
	}

	repositories {
		maven {
			url "https://maven.terraformersmc.com/"
		}
		maven {
			url = "https://api.modrinth.com/maven"
		}
		maven { url = 'https://maven.blamejared.com' }
		maven { url = 'https://maven.minecraftforge.net/' }
		maven { url = "https://cursemaven.com" }
		maven {url = 'https://maven.parchmentmc.org'}
	}

	dependencies {
		minecraft "net.minecraft:minecraft:$rootProject.minecraft_version"
		mappings loom.layered() {
			officialMojangMappings()
			parchment("org.parchmentmc.data:parchment-1.21:2024.07.28@zip")
		}
	}

	java {
		// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
		// if it is present.
		// If you remove this line, sources will not be generated.
		withSourcesJar()

		sourceCompatibility = JavaVersion.VERSION_21
		targetCompatibility = JavaVersion.VERSION_21
	}

	tasks.withType(JavaCompile).configureEach {
		it.options.release = 21
	}
}

allprojects {
	group = rootProject.maven_group
	version = rootProject.mod_version
}
